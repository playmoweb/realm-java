import java.security.MessageDigest

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'de.undercouch.download'
apply plugin: 'net.ltgt.errorprone'

def properties = new Properties()
properties.load(new FileInputStream("${projectDir}/../../dependencies.list"))

ext.coreVersion = properties.getProperty('REALM_SYNC_VERSION')
// empty or comment out this to disable hash checking
ext.coreSha256Hash = properties.getProperty('REALM_SYNC_SHA256')
ext.forceDownloadCore =
        project.hasProperty('forceDownloadCore') ? project.getProperty('forceDownloadCore').toBoolean() : false
// Set the core source code path. By setting this, the core will be built from source. And coreVersion will be read from
// core source code.
ext.coreSourcePath = project.hasProperty('coreSourcePath') ? project.getProperty('coreSourcePath') : null
// The location of core archive.
ext.coreArchiveDir = System.getenv("REALM_CORE_DOWNLOAD_DIR")
if (!ext.coreArchiveDir) {
    ext.coreArchiveDir = ".."
}
ext.coreArchiveFile = rootProject.file("${ext.coreArchiveDir}/realm-sync-android-${project.coreVersion}.tar.gz")
ext.coreDistributionDir = file("${projectDir}/distribution/realm-core/")
ext.coreDir = file(project.coreSourcePath ?
        "${project.coreSourcePath}/android-lib" :
        "${project.coreDistributionDir.getAbsolutePath()}/core-${project.coreVersion}")
ext.ccachePath = project.findProperty('ccachePath') ?: System.getenv('NDK_CCACHE')
ext.lcachePath = project.findProperty('lcachePath') ?: System.getenv('NDK_LCACHE')
// Set to true to enable linking with debug core.
ext.enableDebugCore = project.hasProperty('enableDebugCore') ? project.getProperty('enableDebugCore') : false

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionName version
        project.archivesBaseName = "realm-android-library"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DREALM_CORE_DIST_DIR:STRING=${project.coreDir.getAbsolutePath()}",
                        // FIXME:
                        // This is copied from https://dl.google.com/android/repository/cmake-3.4.2909474-linux-x86_64.zip
                        // because of the android.toolchain.cmake shipped with Android SDK CMake 3.6 doesn't work with our
                        // JNI build currently (lack of lto linking support).
                        // This file should be removed and use the one from Android SDK cmake package when it supports lto.
                        "-DCMAKE_TOOLCHAIN_FILE=${project.file('src/main/cpp/android.toolchain.cmake').path}",
                        "-DENABLE_DEBUG_CORE=$project.enableDebugCore"
                if (project.ccachePath) arguments "-DNDK_CCACHE=$project.ccachePath"
                if (project.lcachePath) arguments "-DNDK_LCACHE=$project.lcachePath"
                if (project.hasProperty('buildTargetABIs') && !project.getProperty('buildTargetABIs').trim().isEmpty()) {
                    abiFilters(*project.getProperty('buildTargetABIs').trim().split('\\s*,\\s*'))
                } else {
                    // armeabi is not supported anymore.
                    abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a', 'mips'
                }
            }
        }

        buildTypes {
            debug {
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments += [ 'realm.suppressWarnings' : 'false' ]
                    }
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    sourceSets {
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
        androidTestObjectServer {
            java.srcDirs += 'src/syncIntegrationTest/java'
            assets.srcDirs += ['src/syncIntegrationTest/assets/']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        // We did strip with cmake for release build.
        // Please, Gradle, you are not that smart! Pleas DO NOT strip debug symbols for debug build!
        doNotStrip "*/*/*.so"
    }

    lintOptions {
        abortOnError false
    }

   flavorDimensions 'api'

    productFlavors {
        base {
            dimension 'api'
            externalNativeBuild {
                cmake {
                    arguments "-DREALM_FLAVOR=base"
                }
            }
            consumerProguardFiles 'proguard-rules-common.pro', 'proguard-rules-base.pro'
        }
        objectServer {
            dimension 'api'
            externalNativeBuild {
                cmake {
                    arguments "-DREALM_FLAVOR=objectServer"
                }
            }
            consumerProguardFiles 'proguard-rules-common.pro', 'proguard-rules-objectServer.pro'
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        // Ignore the objectServer flavour when building from core source.
        if (coreSourcePath && names.contains("objectServer")) {
            variant.ignore = true
        }
    }
}

project.afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Werror'
    }
}

// enable @ParametersAreNonnullByDefault annotation. See https://blog.jetbrains.com/kotlin/2017/08/kotlin-1-1-4-is-out/
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305-annotations=enable"]
    }
}

coveralls.jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"

import io.realm.transformer.RealmTransformer

android.registerTransform(new RealmTransformer())

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {

    compileOnly 'io.reactivex:rxjava:1.1.0'
    compileOnly 'com.google.code.findbugs:findbugs-annotations:3.0.1'

    api "io.realm:realm-annotations:${version}"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.getkeepsafe.relinker:relinker:1.2.2'

    kaptObjectServer project(':realm-annotations-processor')
    objectServerImplementation 'com.squareup.okhttp3:okhttp:3.7.0'

    kaptAndroidTest project(':realm-annotations-processor')
    androidTestImplementation fileTree(dir: 'testLibs', include: ['*.jar'])
    androidTestImplementation 'io.reactivex:rxjava:1.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // specify error prone version to prevent sudden failure
    errorprone 'com.google.errorprone:error_prone_core:2.0.21'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.objectServer.java.srcDirs
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

def betaTag = 'Beta:a:<div style="border-style:solid;border-width:2px">This software is considered in beta phase. ' +
        'It indicates that any public interface can change without prior announcements. ' +
        'Moreover, classes, constructors, and methods annotated as beta are not ' +
        'considered at production quality, and should be used with care.</div>'

task javadoc(type: Javadoc) {
    source android.sourceSets.objectServer.java.srcDirs
    source android.sourceSets.main.java.srcDirs
    source "../../realm-annotations/src/main/java"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        title = "Realm ${project.version}"
        memberLevel = JavadocMemberLevel.PUBLIC
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        locale = 'en_US'
        overview = 'src/overview.html'

        links "https://docs.oracle.com/javase/7/docs/api/"
        links "http://reactivex.io/RxJava/javadoc/"
        linksOffline "https://developer.android.com/reference/", "${project.android.sdkDirectory}/docs/reference"

        tags = [betaTag]
    }
    exclude '**/internal/**'
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    doLast {
        copy {
            from "src/realm-java-overview.png"
            into "$buildDir/docs/javadoc"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task findbugs(type: FindBugs) {
    dependsOn assemble
    group = 'Verification'

    ignoreFailures = false
    effort = "default"
    reportLevel = "medium"
    excludeFilter = file("${projectDir}/../config/findbugs/findbugs-filter.xml")
    classes = files("${projectDir}/build/intermediates/classes")
    source = fileTree('src/main/java/')
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination file("$project.buildDir/findbugs/findbugs-output.xml")
        }
        html {
            destination file("$project.buildDir/findbugs/findbugs-output.html")
        }
    }
}

task pmd(type: Pmd) {
    group = 'Verification'

    source = fileTree('src/main/java')
    ruleSetFiles = files("${projectDir}/../config/pmd/ruleset.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// Configure Checkstyle
// Android sourceSets are not sourceSets, so we can't confgure this with the DSL.
task checkstyle(type: Checkstyle) {
    group = 'Verification'

    source 'src'
    include '*/java/**/*.java'
    // Ignore tests for now.
    exclude '*Test*/**'

    // empty classpath
    classpath = files()
}

checkstyle {
    toolVersion ="7.6"

    configFile = file("${projectDir}/../config/checkstyle/checkstyle.xml")

    def configProps = ['proj.module.dir': projectDir.absolutePath]
    configProperties configProps

    ignoreFailures = true
}
check.dependsOn tasks.checkstyle

artifacts {
    archives javadocJar
    archives sourcesJar
}

def coreDownloaded = false

task downloadCore() {
    group = 'build setup'
    description = 'Download the latest version of Realm Core'
    def isHashCheckingEnabled = {
        return project.hasProperty('coreSha256Hash') && !project.coreSha256Hash.empty
    }

    def calcSha256Hash = { File targetFile ->
        MessageDigest sha = MessageDigest.getInstance("SHA-256")
        Formatter hexHash = new Formatter()
        sha.digest(targetFile.bytes).each { b -> hexHash.format('%02x', b) }
        return hexHash.toString()
    }

    def shouldDownloadCore = {
        if (!project.coreArchiveFile.exists()) {
            return true
        }
        if (project.forceDownloadCore) {
            return true;
        }
        if (!isHashCheckingEnabled()) {
            println "Skipping hash check(empty \'coreSha256Hash\')."
            return false
        }

        def calculatedHash = calcSha256Hash(project.coreArchiveFile)
        if (project.coreSha256Hash.equalsIgnoreCase(calculatedHash)) {
            return false
        }

        println "Existing archive hash mismatch (Expected: ${project.coreSha256Hash.toLowerCase()}" +
                " but got ${calculatedHash.toLowerCase()}). Download new version."
        return true
    }

    doLast {
        if (shouldDownloadCore()) {
            download {
                src "http://static.realm.io/downloads/sync/realm-sync-android-${project.coreVersion}.tar.gz"
                dest project.coreArchiveFile
                onlyIfNewer false
            }
            coreDownloaded = true

            if (isHashCheckingEnabled()) {
                def calculatedHash = calcSha256Hash(project.coreArchiveFile)
                if (!project.coreSha256Hash.equalsIgnoreCase(calculatedHash)) {
                    throw new GradleException("Invalid checksum for file '" +
                            "${project.coreArchiveFile.getName()}'. Expected " +
                            "${project.coreSha256Hash.toLowerCase()} but got " +
                            "${calculatedHash.toLowerCase()}.");
                }
            } else {
                println 'Skipping hash check (empty \'coreSha256Hash\').'
            }
        }
    }
}

task compileCore(group: 'build setup', description: 'Compile the core library from source code') {
    // Build the library from core source code
    doFirst {
        if (!coreSourcePath) {
            throw new GradleException('The coreSourcePath is not set.')
        }
        exec {
            workingDir = coreSourcePath
            commandLine = [
                    "bash",
                    "build.sh",
                    "build-android"
            ]
        }
    }

    // Copy the core tar ball
    doLast {
        copy {
            from "${coreSourcePath}/realm-core-android-${coreVersion}.tar.gz"
            into project.coreArchiveFile.parent
            rename "realm-core-android-${coreVersion}.tar.gz", "realm-sync-android-${coreVersion}.tar.gz"
        }
    }
}

task deployCore(group: 'build setup', description: 'Deploy the latest version of Realm Core') {
    dependsOn {
        coreSourcePath ? compileCore : downloadCore
    }

    // Build with the output from core source dir. No need to deploy anything.
    onlyIf {
        return !coreSourcePath
    }

    outputs.upToDateWhen {
        // Clean up the coreDir if it is newly downloaded
        if (coreDownloaded) {
            return false
        }

        return project.coreDir.exists()
    }

    doLast {
        // Delete all files to avoid multiple copies of the same header file in Android Studio.
        exec {
            commandLine = [
                    'rm',
                    '-rf',
                    project.coreDistributionDir.getAbsolutePath()
            ]
        }
        copy {
            from tarTree(project.coreArchiveFile)
            into project.coreDir
        }
    }
}

publishToMavenLocal.dependsOn assemble
preBuild.dependsOn deployCore

if (project.hasProperty('dontCleanJniFiles')) {
    project.afterEvaluate {
        tasks.all { task ->
            if (task.name.startsWith('externalNativeBuildClean')) {
                task.enabled = false
            }
        }
    }
} else {
    task cleanExternalBuildFiles(type: Delete) {
        delete project.file('.externalNativeBuild')
        // Clean .so files that were created by old build script (realm/realm-jni/build.gradle).
        delete project.file('src/main/jniLibs')
    }
    clean.dependsOn cleanExternalBuildFiles
}

project.afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.externalNativeBuildTasks[0].dependsOn(checkNdk)
        if (project.hasProperty('buildTargetABIs') && project.getProperty('buildTargetABIs').trim().isEmpty()) {
            variant.externalNativeBuildTasks[0].enabled = false
        }
    }
}

task checkNdk() {
    doLast {
        def ndkPathInEnvVariable = System.env.ANDROID_NDK_HOME
        if (!ndkPathInEnvVariable) {
            throw new GradleException("The environment variable 'ANDROID_NDK_HOME' must be set.")
        }
        checkNdk(ndkPathInEnvVariable)

        def localPropFile = rootProject.file('local.properties')
        if (!localPropFile.exists()) {
            // we can skip the checks since 'ANDROID_NDK_HOME' will be used instead.
        } else {
            def String ndkPathInLocalProperties = getValueFromPropertiesFile(localPropFile, 'ndk.dir')
            if (!ndkPathInLocalProperties) {
                throw new GradleException("'ndk.dir' must be set in ${localPropFile.getAbsolutePath()}.")
            }
            checkNdk(ndkPathInLocalProperties)
            if (new File(ndkPathInLocalProperties).getCanonicalPath()
                    != new File(ndkPathInEnvVariable).getCanonicalPath()) {
                throw new GradleException(
                        "The value of environment variable 'ANDROID_NDK_HOME' (${ndkPathInEnvVariable}) and"
                                + " 'ndk.dir' in 'local.properties' (${ndkPathInLocalProperties}) "
                                + ' must point the same directory.')
            }
        }
    }
}

def checkNdk(String ndkPath) {
    def detectedNdkVersion
    def releaseFile = new File(ndkPath, 'RELEASE.TXT')
    def propertyFile = new File(ndkPath, 'source.properties')
    if (releaseFile.isFile()) {
        detectedNdkVersion = releaseFile.text.trim().split()[0].split('-')[0]
    } else if (propertyFile.isFile()) {
        detectedNdkVersion = getValueFromPropertiesFile(propertyFile, 'Pkg.Revision')
        if (detectedNdkVersion == null) {
            throw new GradleException("Failed to obtain the NDK version information from ${ndkPath}/source.properties")
        }
    } else {
        throw new GradleException("Neither ${releaseFile.getAbsolutePath()} nor ${propertyFile.getAbsolutePath()} is a file.")
    }
    if (detectedNdkVersion != project.ndkVersion) {
        throw new GradleException("Your NDK version: ${detectedNdkVersion}."
                + " Realm JNI must be compiled with the version ${project.ndkVersion} of NDK.")
    }
}

static def getValueFromPropertiesFile(File propFile, String key) {
    if (!propFile.isFile() || !propFile.canRead()) {
        return null
    }
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}
