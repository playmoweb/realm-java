apply plugin: 'java'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    implementation "com.squareup:javawriter:2.5.0"
    implementation "io.realm:realm-annotations:${version}"

    testImplementation files('../realm-library/build/intermediates/bundles/baseRelease/classes.jar') // Java projects cannot depend on AAR files
    testImplementation files("${System.properties['java.home']}/../lib/tools.jar") // This is needed otherwise compile-testing won't be able to find it
    testImplementation group:'junit', name:'junit', version:'4.12'
    testImplementation group:'com.google.testing.compile', name:'compile-testing', version:'0.6'
    testImplementation files(file("${System.env.ANDROID_HOME}/platforms/android-26/android.jar"))
}

// for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/io/realm/processor'
    filter(ReplaceTokens, tokens: [version: version])
    outputs.upToDateWhen { false }
}

// Include the generated Version file
sourceSets {
    main {
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

compileJava.dependsOn generateVersionClass
compileTestJava.dependsOn ':realm-library:assembleBaseRelease'